apply plugin: 'java-library'
apply plugin: "kotlin"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxjava2}"
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

task createDayJson(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "com.feature.dayjson.DayJsonMain"
}

task updateDayJson() {
    dependsOn("createDayJson")
    doLast {
        exeDayJsonGitCmd()
    }
}

void exeDayJsonGitCmd() {
    String currentTimeText = getCurrentTimeText()
    println("exeDayJsonGitCmd called...  currentTimeText: " + currentTimeText)
    exeDayJsonGitAdd()
    exeDayJsonGitCommit(currentTimeText)
    exeDayJsonGitFetch()
    exeDayJsonGitRebaseMaster()
    exeDayJsonGitPushMaster()
}

void exeDayJsonGitFetch() {
    println("exeDayJsonGitFetch called...")
    exec {
        workingDir '../'
        commandLine 'git', 'fetch', 'origin'
    }
}

void exeDayJsonGitRebaseMaster() {
    println("exeDayJsonGitRebaseMaster called...")
    exec {
        workingDir '../'
        commandLine 'git', 'rebase', 'origin/master'
    }
}

void exeDayJsonGitAdd() {
    println("exeDayJsonGitAdd called...")
    exec {
        workingDir '../'
        commandLine 'git', 'add', '.'
    }
}

void exeDayJsonGitCommit(String currentTimeText) {
    println("exeDayJsonGitCommit called...")
    exec {
        workingDir '../'
        commandLine 'git', 'commit', '-m', 'update idaily list data ' + currentTimeText
    }
}

void exeDayJsonGitPushMaster() {
    println("exeDayJsonGitPushMaster called...")
    exec {
        workingDir '../'
        commandLine 'git', 'push', 'origin', 'master:master'
    }
}

private static def getCurrentTimeText() {
    String currentTimeText = new Date().format("yyyy-MM-dd hh:mm:ss",
            TimeZone.getTimeZone("GMT+08:00"))
    return currentTimeText != null ? currentTimeText : ""
}
